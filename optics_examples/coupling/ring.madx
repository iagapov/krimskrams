
qf : quadrupole, l=0.1, k1 = 2.0;
qd : quadrupole, l=0.1, k1 = -2.0;
q1s : quadrupole, l=0.05, k1s = 1.0;
q2s : quadrupole, l=0.05, k1s = -1.0;
d0 : drift, l = 0.5;
b1 : sbend, l=1.0, angle=0.01;

sf: sextupole, l=0.01, k2=300;
sd: sextupole, l=0.01, k2=-100;

cell : line = (d0,b1,d0,sf,qf,sf,d0,b1,d0,sd,qd,sd);

hcor1 : hkicker, hkick=0.0001;
vcor1 : vkicker, vkick=0.0000;

ncell : line = (cell, q1s, hcor1, vcor1,
                cell, q2s,
                cell, q1s,
                cell, q2s,
                cell, q1s,
                cell, q2s,
                cell, q1s,
                cell, q2s);

beam, particle=electron, energy=1.0;
use, period=cell;
twiss, chrom;

use, period=ncell;

match, sequence = ncell;
constraint, sequence=ncell, range=#e, mux = 1.15;
constraint, sequence=ncell, range=#e, muy = 1.15;
vary, name=qf->k1, lower=-20, upper=20, step=1.e-6;
vary, name=qd->k1, lower=-20, upper=20, step=1.e-6;
lmdif, calls=5000;
endmatch;

use, period=ncell;
twiss, chrom;


plot, haxis=s, vaxis1=betx, bety, vmin=0., vaxis2 = Dx, colour=100;
plot, haxis=s, vaxis1=mux, muy, vmin=0., colour=100;
plot, haxis=s, vaxis1=x, y, vmin=0., colour=100;


use, period=ncell;
twiss, chrom;


ptc_create_universe;
ptc_create_layout,model=2,method=6,nst=1,exact;


select flag=ptc_twiss,column=s,betx,beta11,bety,beta22,beta12,beta21,mu1,mu2,beta11p,beta21p,beta22p,beta12p,
disp1,disp1p,disp1p2,disp1p3;
ptc_twiss, deltap=0.00, deltap_dependency, closed_orbit,icase=5,file="ncell.tfs",no=6, normal=true, maptable=true;


write, table=nonlin,file="nonlin.tfs";
write, table=map_table,file="map_table.tfs";
